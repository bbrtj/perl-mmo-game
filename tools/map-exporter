#!/usr/bin/env perl

my $dir;
BEGIN {
	use File::Basename;
	$dir = dirname(dirname(__FILE__));
}

use lib "$dir/local/lib/perl5";
use lib "$dir/lib-base";
use lib "$dir/lib";

use Game::LoreLoader;
use Mojo::File qw(path);
use Mojo::JSON qw(to_json);

use header;

my ($file) = @ARGV;
die 'you must specify a file path relative to game-data directory'
	unless defined $file;

Game::LoreLoader->load(path($file)->to_rel(Game::LoreLoader->DIRECTORY));

my $repo = DI->get('lore_data');
my %areas = $repo->load_all_named('Game::Lore::Area')->%*;

my @keys = keys %areas;

die 'File contents are too vague - more than one area found'
	unless @keys < 2;
die 'No areas found. Is this file finished?'
	unless @keys > 0;

my @markers;
foreach my $location ($areas{$keys[0]}->data->children->@*) {
	push @markers, {
		LoreId => $location->id,
		PosX => $location->data->pos_x // rand,
		PosY => $location->data->pos_y // rand,
		ConnectedTo => join ';', map { $_->id } $location->data->connections->@*,
	};
}

my $struct = {
	Markers => \@markers
};

binmode STDOUT, ':utf8';
say to_json $struct;

__END__

=pod

Exports map specified on input. Used in game editor to edit coordinates on a map.

